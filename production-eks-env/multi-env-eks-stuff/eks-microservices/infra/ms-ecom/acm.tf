# Per-environment wildcard cert. Each apply (var.env) owns one cert:
#   *.dev.devopsdozo.livingdevops.org
#   *.prod.devopsdozo.livingdevops.org
# Shop and the other service hosts are one label under that wildcard.

data "aws_route53_zone" "main" {
  name         = var.domain_name
  private_zone = false
}

resource "aws_acm_certificate" "env" {
  domain_name       = "*.${local.env_dns_suffix}"
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = {
    Name        = "${var.env}-ecommerce-wildcard"
    Environment = var.env
  }
}

resource "aws_route53_record" "cert_validation" {
  for_each = {
    for dvo in aws_acm_certificate.env.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = data.aws_route53_zone.main.zone_id
}

resource "aws_acm_certificate_validation" "env" {
  certificate_arn         = aws_acm_certificate.env.arn
  validation_record_fqdns = [for record in aws_route53_record.cert_validation : record.fqdn]
}

locals {
  acm_cert_arn = coalesce(var.acm_cert_arn, aws_acm_certificate_validation.env.certificate_arn)
}
